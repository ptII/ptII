/***semantics_AddSubtract***/
(tomod ADDSUBTRACT is 
  inc ATOMIC-ACTOR .

  class AddSubtract .
      subclass AddSubtract < AtomicActor .
  eq ^ AddSubtract = AtomicActor .

---------------------------------------------------------------------
--- Variable Declaration
---------------------------------------------------------------------

  vars PORTS : Configuration . var REST : ObjectConfiguration .
  var PI : PortId . var EPI : EPortId .
  var E : Exp . vars V : Value . var O : Oid .

---------------------------------------------------------------------
--- Semantics Steps
---------------------------------------------------------------------

*** 0] init

*** 1] prefire

*** 2] portFixPoints 

  ceq portFixPoints(
      < O : AddSubtract | 
         status : enabled,
         ports : < PI : InPort | status : present >
                 < 'output : OutPort | status : unknown > PORTS > 
      REST)
  = 
     portFixPoints(
      < O : AddSubtract | 
         ports : < PI : InPort | > 
                 < 'output : OutPort | status : present, 
                                       value : addsub(< PI : InPort | > PORTS, # 0) > PORTS > 
      REST)
   if allInputDecided(PORTS) .
   
  op allInputDecided : Configuration ~> Bool [frozen] .
  eq allInputDecided(< PI : Port | status : unknown > PORTS) = false .
  eq allInputDecided(PORTS) = true [owise] .
  
  op addsub : Configuration Exp ~> Exp [frozen] .
  eq addsub(< 'plus : InPort | status : present, value : V > PORTS, E)
   = addsub(PORTS, E + V) .
  eq addsub(< 'plus # EPI : InPort | status : present, value : V > PORTS, E)
   = addsub(PORTS, E + V ) .
  eq addsub(< 'minus : InPort | status : present, value : V > PORTS, E)
   = addsub(PORTS, E - V) .
  eq addsub(< 'minus # EPI : InPort | status : present, value : V > PORTS, E)
   = addsub(PORTS, E - V) .
  eq addsub(PORTS, E) = E [owise] .
   
*** 3] postfire

endtom)
/**/
