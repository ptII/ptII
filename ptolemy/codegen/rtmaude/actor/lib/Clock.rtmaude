/***moduleName***/
CLOCK/**/

/***className***/
Clock/**/

/***attr_Clock***/
index : 0/**/

/***sharedBlock***/
(tomod CLOCK is 
  inc ACTOR .

  class Clock | index : Nat .
    subclass Clock < Actor .
  eq ^ Clock = Actor .

  vars PORTS : Configuration . vars PI : PortId .
  var O : Oid . var REST : ObjectConfiguration .
  var AM : AssignMap . var MEM : Memory .
  var VP VO VV : Value . var EP EO EV : Exp .
  var UPDATED : Bool . var NI : Nat .

---------------------------------------------------------------------
--- Semantics Steps
---------------------------------------------------------------------

  eq init(Clock,
    < O : Clock |
      parameters : ('offsets |-> EO ; 'values |-> EV ; AM)
    >)
   = init(^ Clock, < O : Clock | >)
    evt(event(O ! 'output, EV(# 0)), toTime(EO(# 0))) .

  --- set each status of ports to 'unknown'
  eq prefire(Clock, 
    < O : Clock | 
      parameters : ('period |-> EP ; 'offsets |-> EO ; 'values |-> EV ; AM)
    >) 
   = prefire(^ Clock, 
    < O : Clock | 
      store : 'period |-> k(EP) ; 'offsets |-> k(EO) ; 'values |-> k(EV)
    >) .

  --- Note: Clock actors generate events "later" and hence
  --- do not generate output WHEN their ports are in unknown state
  eq portFixPoints(UPDATED,
     < O : Clock | 
      status : enabled,
      ports : < PI : OutPort | status : unknown > PORTS 
     > REST)
   = portFixPoints(true,
     < O : Clock | 
      ports : < PI : OutPort | status : absent > PORTS > REST) .

  --- When should clock generate "next" event?? Maybe when it has something
  --- in its current output port??? 
  --- Assumed exactly ONE outport per clock!
  --- In fact, the name of the outport is 'output' (ref. Ptolemy Documentation )

  ---FIXME: Offset could be Infinity
  ceq postfire(Clock,
    < O : Clock | 
      ports : < PI : OutPort | status : present > PORTS, 
      store : ('offsets |-> VO ; 'values |-> VV ; MEM),
      index : NI
    >) 
  = evt(event(O ! PI, VV(#(s NI))), toTime((VO(#(s NI))) - (VO(# NI))))
    postfire(^ Clock, < O : Clock | index : s(NI) >)
   if ((# NI + # 1) lessThan (VO .. 'length(()) )) == # true .

  ceq postfire(Clock,
    < O : Clock | 
      ports : < PI : OutPort | status : present >  PORTS, 
      store : ('period |-> VP ; 'offsets |-> VO ; 'values |-> VV ; MEM),
      index : NI
    >) 
  = evt(event(O ! PI, VV(# 0)), toTime((VP - (VO(# NI))) + (VO(# 0))))
    postfire(^ Clock, < O : Clock | index : 0 >)
   if ((# NI + # 1) equals (VO .. 'length(()) )) == # true 
    /\ (VP lessThan Infinity) == # true .
endtom)
/**/
