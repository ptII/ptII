/***semantics_TimedDelay***/
(tomod DELAY-ACTOR  is 
  inc ACTOR .

  class Delay .  subclass Delay < Actor .
  eq ^ Delay = Actor .

  vars PORTS : Configuration .  vars PI PI' : PortId .
  var REST : ObjectConfiguration . vars O : Oid .
  var E : Exp . var V TV : Value .  vars T : Time .
  var AM : AssignMap . var MEM : Memory .

---------------------------------------------------------------------
--- Semantics Steps
---------------------------------------------------------------------

  --- set each status of ports to 'unknown'
  eq prefire(Delay, 
      < O : Delay | parameters : ('delay |-> E ; AM) >) 
   = prefire(^ Delay, 
      < O : Delay | store : 'delay |-> k(E) >) .

  --- Note: Delay actors generate events "later" and hence
  --- do not generate output WHEN their ports are in unknown state
  eq portFixPoints(
     < O : Delay | 
      status : enabled,
      ports :  < PI : OutPort | status : unknown > PORTS 
    > 
    REST) 
   = portFixPoints(
     < O : Delay |
       ports : < PI : OutPort | status : absent > PORTS > REST) .

  *** execute a step for delay operators ... : do NOT generate new output.
  *** IF input, then generate new event, otherwise do nothing. 
  --- Note that generated events are now just messages, which will be dilivered
  --- to globalEventQueue actor.
  eq postfire(Delay, 
    < O : Delay | 
      ports : ( 
        < 'input : InPort | status : present, value : V >  
        < 'output : OutPort | > PORTS), 
      store : 'delay |-> TV ; MEM
    >)
  = relative-evt(event(O ! 'output, V), toTime(TV)) 
    postfire(^ Delay, < O : Delay | >) .
endtom)
/**/