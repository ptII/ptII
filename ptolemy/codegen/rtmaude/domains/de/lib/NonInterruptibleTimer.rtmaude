/***moduleName***/
NONINTERRUPTIBLE-TIMER/**/

/***className***/
NonInterruptibleTimer/**/

/***sharedBlock***/
(tomod NONINTERRUPTIBLE-TIMER  is
  inc ACTOR .

  class NonInterruptibleTimer |
        processing : Bool,
        wait-queue : TimeList,
        output-value : VarId .
    subclass NonInterruptibleTimer < Actor .

  sort TimeList .
  subsort Time < TimeList .
  op emptyList : -> TimeList .
  op __ : TimeList TimeList -> TimeList [ctor assoc id: emptyList] .

  vars REST PORTS : Configuration .  vars O : Oid .  vars PI PI' : PortId .
  var PS : PortStatus .
  var V : Value .  vars T  : Time . vars TL UQ : TimeList .
  vars UPDATED FLAG : Bool .
  var AM : AssignMap . var MEM : Memory .
  var VI : VarId . var NV : NumberValue . var E : Exp .

---------------------------------------------------------------------
--- Semantics Steps
---------------------------------------------------------------------

  --- set each status of ports to 'unknown'
  eq initActor(NonInterruptibleTimer, 
    < O : NonInterruptibleTimer | 
      output-value : VI,
      variables : VI |-> E ; AM
    >)
   = initActor(Actor, 
     < O : NonInterruptibleTimer | 
       store : eval(VI, k(E))
     >) .

  eq portFixPoints(UPDATED,
     < O : NonInterruptibleTimer |
        status : enabled,
        ports :  < PI : OutPort | status : unknown > PORTS
    >
    REST)
   = portFixPoints(true,
     < O : NonInterruptibleTimer |
        ports : < PI : OutPort | status : absent > PORTS
    >
    REST) .

  ceq executeStep(NonInterruptibleTimer,
    < O : NonInterruptibleTimer |
        processing : true,
        wait-queue : TL,
        output-value : VI,
        store : VI |-> V ; MEM,
        ports : (
            < 'input : InPort | status : PS, value : NV >
            < 'output : OutPort | status : present >
            PORTS)
    >,
    REST)
  = if UQ =/= emptyList then
        evt(event(O ! 'output, V), first(UQ))
        executeStep(Actor,
        < O : NonInterruptibleTimer |
            wait-queue : rest(UQ)
        >,
        REST)
    else
        executeStep(Actor,
        < O : NonInterruptibleTimer |
            processing : false
        >,
       REST)
    fi
  if UQ := (if PS == present then (TL toTime(NV)) else TL fi) .

  eq executeStep(NonInterruptibleTimer,
    < O : NonInterruptibleTimer |
        processing : FLAG,
        wait-queue : TL,
        output-value : VI,
        store : VI |-> V ; MEM,
        ports : (
            < 'input : InPort | status : present, value : NV >
            < 'output : OutPort | status : absent >
            PORTS)
    >,
    REST)
  = if FLAG then
        executeStep(Actor,
        < O : NonInterruptibleTimer |
            wait-queue : (TL toTime(NV))
        >,
        REST)
    else
        evt(event(O ! 'output, V), toTime(NV))
        executeStep(Actor,
        < O : NonInterruptibleTimer |
            processing : true
        >,
        REST)
    fi .

  eq executeStep(NonInterruptibleTimer,
    < O : NonInterruptibleTimer |
        ports : (
            < 'input : InPort | status : absent >
            < 'output : OutPort | status : absent >
            PORTS)
    >,
    REST)
  = executeStep(Actor, < O : NonInterruptibleTimer | >, REST) .

  op first : TimeList ~> Time .
  eq first(T TL) = T .

  op rest : TimeList ~> TimeList .
  eq rest(T TL) = TL .
endtom)
/**/
