/***attr_NonInterruptibleTimer***/
processing : false,
wait-queue : emptyList/**/

/***semantics_NonInterruptibleTimer***/
(tomod NONINTERRUPTIBLE-TIMER  is
  inc ACTOR .

  class NonInterruptibleTimer |
        processing : Bool,
        wait-queue : TimeList .
    subclass NonInterruptibleTimer < Actor .
  eq ^ NonInterruptibleTimer = Actor .

  sort TimeList .
  subsort Time < TimeList .
  op emptyList : -> TimeList .
  op __ : TimeList TimeList -> TimeList [ctor assoc id: emptyList] .

  vars PORTS : Configuration . vars PI PI' : PortId . var PS : PortStatus .
  var REST : ObjectConfiguration . vars O : Oid .
  var V : Value .  vars T  : Time . vars TL UQ : TimeList .
  var AM : AssignMap . var MEM : Memory .
  var NV : NumberValue . var E : Exp .
  vars UPDATED FLAG : Bool .

---------------------------------------------------------------------
--- Semantics Steps
---------------------------------------------------------------------

  --- set each status of ports to 'unknown'
  eq prefire(NonInterruptibleTimer, 
    < O : NonInterruptibleTimer | 
      parameters : 'value |-> E ; AM
    >)
   = prefire(^ NonInterruptibleTimer, 
     < O : NonInterruptibleTimer | 
       store : 'value |-> k(E)
     >) .

  eq portFixPoints(UPDATED,
     < O : NonInterruptibleTimer |
        status : enabled,
        ports :  < PI : OutPort | status : unknown > PORTS
    >
    REST)
   = portFixPoints(true,
     < O : NonInterruptibleTimer |
        ports : < PI : OutPort | status : absent > PORTS
    >
    REST) .

  ceq postfire(NonInterruptibleTimer,
    < O : NonInterruptibleTimer |
        processing : true,
        wait-queue : TL,
        store : 'value |-> V ; MEM,
        ports : (
            < 'input : InPort | status : PS, value : NV >
            < 'output : OutPort | status : present >
            PORTS)
    >)
  = if UQ =/= emptyList then
        evt(event(O ! 'output, V), first(UQ))
        postfire(^ NonInterruptibleTimer,
        < O : NonInterruptibleTimer |
            wait-queue : rest(UQ)
        >)
    else
        postfire(^ NonInterruptibleTimer,
        < O : NonInterruptibleTimer |
            processing : false
        >)
    fi
  if UQ := (if PS == present then (TL toTime(NV)) else TL fi) .

  eq postfire(NonInterruptibleTimer,
    < O : NonInterruptibleTimer |
        processing : FLAG,
        wait-queue : TL,
        store : 'value |-> V ; MEM,
        ports : (
            < 'input : InPort | status : present, value : NV >
            < 'output : OutPort | status : absent >
            PORTS)
    >)
  = if FLAG then
        postfire(^ NonInterruptibleTimer,
        < O : NonInterruptibleTimer |
            wait-queue : (TL toTime(NV))
        >)
    else
        evt(event(O ! 'output, V), toTime(NV))
        postfire(^ NonInterruptibleTimer,
        < O : NonInterruptibleTimer |
            processing : true
        >)
    fi .

  op first : TimeList ~> Time .
  eq first(T TL) = T .

  op rest : TimeList ~> TimeList .
  eq rest(T TL) = TL .
endtom)
/**/
